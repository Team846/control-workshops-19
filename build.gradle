buildscript {
    ext.kotlin_version = '1.3.30'
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url 'http://repo.kunalsheth.info/' }
        mavenLocal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'info.kunalsheth.units:plugin:5.2.4'
        classpath 'edu.wpi.first:GradleRIO:2019.4.1'
    }
}

apply plugin: 'idea'
apply plugin: 'kotlin-platform-jvm'
apply plugin: 'edu.wpi.first.GradleRIO'

allprojects {
    group 'com.lynbrookrobotics'
    version '1.0.0'
    repositories.jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'

    implementation project(':uoms:jvm')

    implementation wpi.deps.wpilib()
    implementation wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
}

deploy {
    targets {
        roboRIO("roborio") {
            team = 846
        }
    }
    artifacts {
        frcJavaArtifact(project.name) {
            targets << "roborio"
            jvmArgs << '-XX:+UseG1GC' << '-XX:MaxGCPauseMillis=75' << '-Xmx100M'
//          jvmArgs << '-verbose:class'
//          jvmArgs << '-verbosegc'
//          arguments << 'myCustomArgs'
//          debug = true
//          debugPort = 8348
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs += ["-XXLanguage:+InlineClasses"]
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs += ["-XXLanguage:+InlineClasses"]
}

jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest('com.lynbrookrobotics.workshops.FunkyRobotKt')
}